plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}
allprojects {
		
	group = 'br.gabrielsmartins.smartpayment'
	version = '0.0.1-SNAPSHOT'
	
	sourceCompatibility = '11'
	targetCompatibility = '11'

	ext {
		springBootVersion = '2.3.2.RELEASE'
		springCloudVersion = 'Hoxton.SR6'
		lombokVersion = '1.18.12'
		junitVersion = '5.6.2'
		junitPlatformVersion = '1.6.2'
		assertjVersion = '3.16.1'
		apacheCommons3Version = '3.9'
		mapStructVersion = '1.3.1.Final'
		mockitoVersion = '3.3.3'
		archunitVersion = '0.14.1'
		h2Version = '1.4.200'
		mysqlVersion = '8.0.18'
		logstashVersion = '6.4'
	}

	repositories {
		mavenCentral()
	}
}

subprojects {
    apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	dependencies {
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
		testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
		testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
		testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
		testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion

		implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
		testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
		annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
		testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

		implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommons3Version
		
		implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: logstashVersion

		implementation group: 'org.mapstruct', name: 'mapstruct', version: mapStructVersion
		annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: mapStructVersion

		implementation 'org.springframework.boot:spring-boot-starter'

		implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
		implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
			mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
		}
	}

	test {
		useJUnitPlatform()
		finalizedBy(jacocoTestReport)
	}

	jacoco {
		toolVersion = '0.8.5'
	}

	jacocoTestReport {
		reports {
			html.enabled = true
			xml.enabled = true
			csv.enabled = false
			html.destination file("${buildDir}/jacocoHtml")
		}
	}
}
